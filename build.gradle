/*
 * build.gradle
 *
 * Copyright by toolarium-jer, all rights reserved.
 */
plugins {
  id 'com.github.johnrengelman.shadow' version '7.0.0'
}

apply from: "https://raw.githubusercontent.com/toolarium/common-gradle-build/master/gradle/common.gradle"


/***************************************************************************************
 * define the main class name
 ***************************************************************************************/
mainClassName = "com.github.toolarium.jer.Main"


// Minimizing an shadow JAR
def excludedDependenciesGroups = [ //'com.google.code.findbugs'
]
def excludedDependenciesArtifacts = [ //'logback-classic', 'jboss-logging'
]

shadowJar {
  minimize {
      exclude(dependency('org.fusesource.jansi:.*:.*'))
      exclude(dependency('ch.qos.logback:.*:.*'))
  }

  dependencies {
      exclude(dependency {
            if (excludedDependenciesGroups.contains(it.moduleGroup) || excludedDependenciesArtifacts.contains(it.moduleName)) {
                logInfo("> Exclude dependency ["+it.moduleName+"]/["+it.moduleGroup + "]")
                return true;
            }
            logInfo("> Keep dependency ["+it.moduleName+"]/["+it.moduleGroup + "]")
            return false
       })
    }
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}


/****************************************************************************************
 * Define project dependencies
 ****************************************************************************************/
dependencies {
    // see https://docs.gradle.org/current/userguide/java_library_plugin.html

    // logging
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'ch.qos.logback:logback-classic:1.2.11'
    implementation "com.github.toolarium:toolarium-system-command:0.1.0"


    // CLI
    // https://mvnrepository.com/artifact/org.fusesource.jansi/jansi
    implementation 'org.fusesource.jansi:jansi:2.4.0'

    implementation 'info.picocli:picocli:4.7.0'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.0'
}
